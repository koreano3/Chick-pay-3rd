locals {
  common_tags = {
    Project     = "chick-pay-3rd"
    Environment = "dev"
    Owner       = "sujin"
  }
}

terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 5.0"
    }
    kubernetes = {
      source  = "hashicorp/kubernetes"
      version = "~> 2.0"
    }
  }
}

provider "aws" {
  region = "ap-northeast-2"
}

# VPC 정보 불러오기
data "terraform_remote_state" "vpc" {
  backend = "local"
  config = {
    path = "../../vpc/terraform.tfstate"
  }
}

# EKS 클러스터 생성
module "eks" {
  source  = "terraform-aws-modules/eks/aws"
  version = "~> 20.0"

  cluster_name    = var.cluster_name
  cluster_version = var.cluster_version

  cluster_endpoint_public_access  = true
  cluster_endpoint_private_access = true

  vpc_id     = data.terraform_remote_state.vpc.outputs.vpc_id
  subnet_ids = data.terraform_remote_state.vpc.outputs.private_subnet_ids

  enable_irsa                              = true
  enable_cluster_creator_admin_permissions = true

  eks_managed_node_groups = {
    service-nodes = {
      instance_types = ["t3.medium"]
      desired_size   = 2
      min_size       = 1
      max_size       = 3

      iam_role_additional_policies = {
        ebs_csi = "arn:aws:iam::aws:policy/AmazonEBSCSIDriverPolicy"
      }
    }
  }

  tags = merge(local.common_tags, {
    Name    = var.cluster_name
    Purpose = "msa-service"
    Service = "EKS"
  })
}

# 클러스터 정보 불러오기
data "aws_eks_cluster" "this" {
  name       = module.eks.cluster_name
  depends_on = [module.eks]
}

data "aws_eks_cluster_auth" "this" {
  name = module.eks.cluster_name
}

# kubernetes provider 설정
provider "kubernetes" {
  alias                  = "eks"
  host                   = try(data.aws_eks_cluster.this.endpoint, "https://dummy")
  token                  = try(data.aws_eks_cluster_auth.this.token, "dummy-token")
  cluster_ca_certificate = try(base64decode(data.aws_eks_cluster.this.certificate_authority[0].data), "dummy-ca")
}

resource "null_resource" "wait_for_eks" {
  depends_on = [module.eks]
}

